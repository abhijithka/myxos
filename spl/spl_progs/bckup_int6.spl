alias physicalSP S0;
physicalSP = [PTBR+((SP/512)*2)]*512 + (SP%512);
alias sysCallNo S1;
sysCallNo = [physicalSP-1];
if (sysCallNo == 9) then
	print "	Enter exec";
	alias filename S2;
	filename = [physicalSP-3];
	alias i S3;
	alias bb S4;
	i = 0;
	while (i<512) do
		if ([FAT+i]==filename) then
            bb = [FAT+i+2];
			break ;
		endif;
		i = i+8;
	endwhile;
	if (i>=512) then
		[physicalSP-2]=-1;
		ireturn;
	endif;
	load (1, bb);
	alias cd_blcks S5;
	cd_blcks = 0;
	i = 512;
	while ((i < 1024)&&([i] != -1)) do
		cd_blcks = cd_blcks + 1;
	    i = i+1;
	endwhile;
	alias cd_pages S6;
	cd_pages = 0;
	i = 0;
	while (i < 8) do
		if ([PTBR+i] != -1) then
			cd_pages = cd_pages+1;
		else
			break;
		endif;
		i = i+2;
	endwhile ;
	alias j S7;
	if (cd_pages > cd_blcks) then
		i = PTBR+cd_blcks;
		j = cd_blcks;
		while (j<cd_pages) do
			[1280+[i]] = 0;
			[i] = -1;
			[i+1] = "00";
			j = j+1;
			i = i+2;
		endwhile;
	endif;
	if (cd_blcks > cd_pages) then
		i = 1280;
		j = cd_pages;
		while((j<cd_blcks)&&(i<FILE_TABLE)) do
			if ([i]==0) then
				j = j+1;
			endif;
			i = i+1;
		endwhile;
		if (i==FILE_TABLE) then
			[physicalSP-2]=-1;
			ireturn;
		endif;
		i = 1280;
		j = cd_pages;
		while((j<cd_blcks)&&(i<FILE_TABLE)) do
			if ([i]==0) then
				[i] = 1;
				[PTBR+2*j] = i-1280;
				[PTBR+2*j+1] = "01";
				j = j+1;
			endif;
			i = i+1;
		endwhile;
	endif;
	i = 0;
	j = 0;
	while (i<cd_blcks) do
		load([PTBR+j],[512+i]);
	    i = i+1;
	    j = j+2;
	endwhile;
    alias cur_pcb S8;
	cur_pcb = READY_LIST+((PTBR-PAGE_TABLE)/8)*32;
    i = 15;
    alias oft_index S9;
    while (i<30) do
		oft_index = [cur_pcb+i];
		if (oft_index != -1) then
			[oft_index+1] = [oft_index+1]-1;
			if ([oft_index+1]==0) then
				[oft_index] = -1;
			endif;
		endif;
		[cur_pcb+i]=-1;
		[cur_pcb+i+1]=0;
		i = i+2;
	endwhile;
	SP = 3*512;
	[physicalSP]=0;
	[physicalSP-2]=0;
	print "	Exit exec";
	ireturn;
endif;

if (sysCallNo == 11) then
	if ((PTBR>1280) || (PTBR<PAGE_TABLE)) then
		[physicalSP-1] = -1;
		ireturn;
	endif;
	[physicalSP-2] = (PTBR-PAGE_TABLE)/8;
	ireturn;
endif;

if (sysCallNo == 12) then
	alias cur_pcb S10;
	cur_pcb = (READY_LIST+((PTBR-PAGE_TABLE)/8)*32);
	alias ppid S11;
	ppid = [cur_pcb+31];
	if ((ppid<0)||(ppid>31)) then
		[physicalSP-2]=-1;
		ireturn;
	endif;
	[physicalSP-2] = ppid;
	ireturn;
endif;
